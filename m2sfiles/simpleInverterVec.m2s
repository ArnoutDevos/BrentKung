**********************************
**** INVERTER: simple example
**********************************

* Simulation setup
*------------------
*Some simulation options
.options post nomod
.option opts fast parhier=local

*Add the 45nm CMOS library. Notice how the path is generated by MATLAB
.lib '$< strcat(pwd, '/Resources/Technology/tech_wrapper.lib') >$ ' tt

*Add the .vec file
.vec '$< strcat(pwd, '/m2sfiles/simpleInverterVec.vec')>$'

.tran 0.001n 5n *Do a transient simulation, final time = 5ns
.probe v i

.param supply = $globals.supply


* Voltage sources
*-----------------
Vdd vdd vss supply
Vss vss 0 0 


* Simulated circuit
*-------------------
*Creation of a NOT gate in the circuit
*Xnot1 in out vdd vss MYNOT
Xnand1 in1 in2 out vdd vss MYNAND

Cap out vss 10f

* Subcircuits
*-------------
.SUBCKT MYNOT input output vdd vss multfac='1'
xM1 output input vss vss MOSN w='multfac*120e-9' l='45e-9'
xM2 output input vdd vdd MOSP w='multfac*2*120e-9' l='45e-9'
.ENDS MYNOT

.SUBCKT MYNAND input1 input2 output vdd vss multfac='1'
xM1 output input1 vdd vdd MOSP w='multfac*120e-9' l='45e-9'
xM2 output input2 vdd vdd MOSP w='multfac*120e-9' l='45e-9'
xM3 output input1 n1  vss MOSN w='multfac*120e-9' l='45e-9'
xM4 n1	   input2 vss vss MOSN w='multfac*120e-9' l='45e-9'
.ENDS MYNAND

.SUBCKT MYNOR input1 input2 output vdd vss multfac='1'
xM1 n1 	   input1 vdd vdd MOSP w='multfac*120e-9' l='45e-9'
xM2 output input2 n1  vdd MOSP w='multfac*120e-9' l='45e-9'
xM3 output input1 vss vss MOSN w='multfac*120e-9' l='45e-9'
xM4 output input2 vss vss MOSN w='multfac*120e-9' l='45e-9'
.ENDS MYNOR

.END



