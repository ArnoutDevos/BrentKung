******************************
**** 16b Brent-Kung adder ****
******************************
.param supply = $globals.supply
.param halfsupply = $globals.supply/2


* Some simulation options
*-------------------------
.options post nomod
.option opts fast parhier=local

.lib '$< strcat(pwd, '/Resources/Technology/tech_wrapper.lib') >$ ' tt
.tran 0.005n 16n
.vec '$< strcat(pwd, '/m2sfiles/Adder16b.vec')>$'

.probe i

Vdd vdd vss supply
Vdd2 vdd2 vss supply
Vss vss 0 0 

* Actual circuit
*----------------
$ n = 16;
$ for j = 0:n-1
    xNOTa$j  a$j  aN$j     vdd vss MYNOT
    xNOTaN$j aN$j a_buff$j vdd vss MYNOT
    xNOTb$j  b$j  bN$j     vdd vss MYNOT
    xNOTbN$j bN$j b_buff$j vdd vss MYNOT
$ end


Xadder $xbus('a_buff',0:15) $xbus('b_buff',0:15) $xbus('s',0:16) vdd vss ADDER

   
$ for i = 1:16
    xNOT$i  s$i sN$i vdd2 vss MYNOT multfac = 16
$ end



* Brent-Kung Adder subcircuit
*-----------------------------
.SUBCKT ADDER a0 a1 a2 a3 a4 a5 a6 a7 a8 a9 a10 a11 a12 a13 a14 a15 b0 b1 b2 b3 b4 b5 b6 b7 b8 b9 b10 b11 b12 b13 b14 b15 s0 s1 s2 s3 s4 s5 s6 s7 s8 s9 s10 s11 s12 s13 s14 s15 s16 vdd vss
    
    xGenProp0 a0 b0 gen0 prop0 vdd vss GenPropInv
    xGenProp1 a1 b1 gen1 prop1 vdd vss GenPropInv
    xGenProp2 a2 b2 gen2 prop2 vdd vss GenPropInv
    xGenProp3 a3 b3 gen3 prop3 vdd vss GenPropInv
    xGenProp4 a4 b4 gen4 prop4 vdd vss GenPropInv
    xGenProp5 a5 b5 gen5 prop5 vdd vss GenPropInv
    xGenProp6 a6 b6 gen6 prop6 vdd vss GenPropInv
    xGenProp7 a7 b7 gen7 prop7 vdd vss GenPropInv
    xGenProp8 a8 b8 gen8 prop8 vdd vss GenPropInv
    xGenProp9 a9 b9 gen9 prop9 vdd vss GenPropInv
    xGenProp10 a10 b10 gen10 prop10 vdd vss GenPropInv
    xGenProp11 a11 b11 gen11 prop11 vdd vss GenPropInv
    xGenProp12 a12 b12 gen12 prop12 vdd vss GenPropInv
    xGenProp13 a13 b13 gen13 prop13 vdd vss GenPropInv
    xGenProp14 a14 b14 gen14 prop14 vdd vss GenPropInv
    xGenProp15 a15 b15 gen15 prop15 vdd vss GenPropInv

    xNOTprop0	prop0  prop0_inv	vdd vss MYNOTSLOW
    *xNOTprop1	prop1  prop1_inv	vdd vss MYNOTSLOW
    *xNOTprop2	prop2  prop2_inv	vdd vss MYNOTSLOW
    *xNOTprop3	prop3  prop3_inv	vdd vss MYNOTSLOW
    
    xDotOperator_a_0 gen0 	gen1 prop1 gen1_0 	  vdd vss DotOperatorLowerInv multfac='1'
	xBUF1	gen1_0  gen1_0_inv	vdd vss MYNOTSLOW
    xDotOperator_a_1 gen2 prop2	gen3 prop3 gen3_2 prop3_2 vdd vss DotOperatorUpperInv 
    xDotOperator_a_2 gen4 prop4 gen5 prop5 gen5_4 prop5_4 vdd vss DotOperatorUpperInv
    xDotOperator_a_3 gen6 prop6 gen7 prop7 gen7_6 prop7_6 vdd vss DotOperatorUpperInv
    xDotOperator_a_4 gen8 prop8 gen9 prop9 gen9_8 prop9_8 vdd vss DotOperatorUpperInv
    xDotOperator_a_5 gen10 prop10 gen11 prop11 gen11_10 prop11_10 vdd vss DotOperatorUpperInv
    xDotOperator_a_6 gen12 prop12 gen13 prop13 gen13_12 prop13_12 vdd vss DotOperatorUpperInv
    xDotOperator_a_7 gen14 prop14 gen15 prop15 gen15_14 prop15_14 vdd vss DotOperatorUpperInv

    xDotOperator_b_0 gen1_0	    gen3_2 prop3_2 gen3_0	  vdd vss DotOperatorLower multfac='1'
	xBUF2	gen3_0  gen3_0_inv	vdd vss MYNOTSLOW
	xBUF3	gen3_0_inv  gen3_0_b	vdd vss MYNOTSLOW
    xDotOperator_b_1 gen5_4 prop5_4 gen7_6 prop7_6 gen7_4 prop7_4 vdd vss DotOperatorUpper
    xDotOperator_b_2 gen9_8 prop9_8 gen11_10 prop11_10 gen11_8 prop11_8 vdd vss DotOperatorUpper
    xDotOperator_b_3 gen13_12 prop13_12 gen15_14 prop15_14 gen15_12 prop15_12 vdd vss DotOperatorUpper

    xDotOperator_c_0 gen3_0	    gen7_4 prop7_4 gen7_0 		  vdd vss DotOperatorLowerInv multfac='1'
    xDotOperator_c_1 gen11_8 prop11_8 gen15_12 prop15_12 gen15_8 prop15_8 vdd vss DotOperatorUpperInv

    xDotOperator_d_0 gen7_0_b	    gen15_8 prop15_8 gen15_0	      vdd vss DotOperatorLowerInv

    	xNOT7_1	gen7_0  gen7_0_inv	vdd vss MYNOTSLOW
	xNOT7_2	gen7_0_inv  gen7_0_b	vdd vss MYNOTSLOW

	xNOT11_8g	gen11_8  gen11_8_inv	vdd vss MYNOTSLOW
	xNOT11_8p	prop11_8  prop11_8_inv	vdd vss MYNOTSLOW
    xDotOperator_l_1 gen7_0	    gen11_8_inv prop11_8_inv gen11_0	      vdd vss DotOperatorLower multfac='1'
	xNOT7	gen11_0  gen11_0_inv	vdd vss MYNOTSLOW

    xDotOperator_m_1 gen3_0_inv	    gen5_4 prop5_4 gen5_0 	  vdd vss DotOperatorLower
    xDotOperator_m_2 gen7_0_b	    gen9_8 prop9_8 gen9_0	  vdd vss DotOperatorLower
	xNOT13_g	gen13_12  gen13_12_inv	vdd vss MYNOTSLOW
	xNOT13_p	prop13_12  prop13_12_inv	vdd vss MYNOTSLOW
    xDotOperator_m_3 gen11_0 gen13_12_inv prop13_12_inv gen13_0 vdd vss DotOperatorLowerInv multfac='1.5'

    	xNOT9_1	gen1_0  gen1_0_inv	vdd vss MYNOTSLOW
	xNOT9_2	gen1_0_inv  gen1_0_b	vdd vss MYNOTSLOW
	xNOT9_3	gen2  gen2_inv		vdd vss MYNOTSLOW

    xDotOperator_n_1 gen1_0_b	    gen2_inv prop2 gen2_0	      vdd vss DotOperatorLower
    xDotOperator_n_2 gen3_0_b	    gen4 prop4 gen4_0	      vdd vss DotOperatorLowerInv
    xDotOperator_n_3 gen5_0	    gen6 prop6 gen6_0	      vdd vss DotOperatorLowerInv
    xDotOperator_n_4 gen7_0_inv	    gen8 prop8 gen8_0	      vdd vss DotOperatorLowerInv
    xDotOperator_n_5 gen9_0	    gen10 prop10 gen10_0      vdd vss DotOperatorLowerInv
	xNOT12g	gen12  gen12_inv	vdd vss MYNOTSLOW
	xNOT12p	gen12  prop12_inv	vdd vss MYNOTSLOW
    xDotOperator_n_6 gen11_0_inv gen12_inv prop12_inv gen12_0 vdd vss DotOperatorLower
	xNOT14g	gen14  gen14_inv	vdd vss MYNOTSLOW
	xNOT14p	prop14  prop14_inv	vdd vss MYNOTSLOW
    xDotOperator_n_7 gen13_0 gen14_inv prop14_inv gen14_0 vdd vss DotOperatorLower multfac='2'

	*REMOVE s0 and s16 XOR gates with .connect
    *xXOR_0 prop0 vss    s0 vdd vss MYTXNOR
	.connect prop0_inv s0
    xXOR_1 prop1 gen0   s1 vdd vss MYTXOR
    xXOR_2 prop2 gen1_0_inv s2 vdd vss MYTXOR 
    xXOR_3 prop3 gen2_0 s3 vdd vss MYTXOR 
    xXOR_4 prop4 gen3_0_inv s4 vdd vss MYTXNOR 
    xXOR_5 prop5 gen4_0 s5 vdd vss MYTXNOR 
    xXOR_6 prop6 gen5_0 s6 vdd vss MYTXOR 
    xXOR_7 prop7 gen6_0 s7 vdd vss MYTXNOR 
    xXOR_8 prop8 gen7_0_inv s8 vdd vss MYTXOR 
    xXOR_9 prop9 gen8_0 s9 vdd vss MYTXNOR 
    xXOR_10 prop10 gen9_0 s10 vdd vss MYTXOR 
    xXOR_11 prop11 gen10_0 s11 vdd vss MYTXNOR 
    xXOR_12 prop12 gen11_0_inv s12 vdd vss MYTXNOR 
    xXOR_13 prop13 gen12_0 s13 vdd vss MYTXOR
	xNOT13_0g	gen13_0  gen13_0_inv	vdd vss MYNOTSLOW
    xXOR_14 prop14 gen13_0_inv s14 vdd vss MYTXOR 
    xXOR_15 prop15 gen14_0 s15 vdd vss MYXOR  multfac='4'  
    xXOR_16   gen15_0 s16 vdd vss MYNOTSLOW
.ENDS ADDER




* Other subcircuits
*-------------------

.SUBCKT MYNAND inputA inputB output vdd vss multfac='1'
    xM1 output inputA int vss MOSN w='multfac*120e-9*2' l='45e-9'
    xM2 int    inputB vss vss MOSN w='multfac*120e-9*2' l='45e-9'
    xM3 output inputA vdd vdd MOSP w='multfac*120e-9*2' l='45e-9'
    xM4 output inputB vdd vdd MOSP w='multfac*120e-9*2' l='45e-9'
.ENDS MYNAND

.SUBCKT MYNOT input output vdd vss multfac='1'
    xM1 output input vss vss MOSN w='multfac*120e-9' l='45e-9'
    xM2 output input vdd vdd MOSP w='multfac*2*120e-9' l='45e-9'
.ENDS MYNOT

.SUBCKT MYXOR inputA inputB  output vdd vss multfac='1'
    xNOT1 inputA inputNA vdd vss MYNOTSLOW
    xNOT2 inputB inputNB vdd vss MYNOTSLOW

    xM1 np1     inputA  vdd vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM2 np1     inputB  vdd vdd MOSP w='multfac*4*120e-9' l='45e-9'
    xM3 output  inputNA np1 vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM4 output  inputNB np1 vdd MOSP w='multfac*4*120e-9' l='45e-9'

    xM5 output  inputA  nn1 vss MOSN w='multfac*1*120e-9' l='45e-9'
    xM6 output  inputNA nn2 vss MOSN w='multfac*1*120e-9' l='45e-9'
    xM7 nn1     inputB  vss vss MOSN w='multfac*1*120e-9' l='45e-9'
    xM8 nn2     inputNB vss vss MOSN w='multfac*1*120e-9' l='45e-9'
.ENDS MYXOR

.SUBCKT MYAOI A B C Out vdd vss multfac='1'
    xM1 Out     B  np1 vdd MOSP w='1*120e-9' l='45e-9'
    xM2 Out     C  np1 vdd MOSP w='1*120e-9' l='45e-9'
    xM3 np1     A  vdd vdd MOSP w='multfac*2*120e-9' l='45e-9'
    
    xM5 Out     A  vss vss MOSN w='multfac*2*120e-9' l='45e-9'
    xM6 Out     C  nn1 vss MOSN w='1*120e-9' l='45e-9'
    xM7 nn1     B  vss vss MOSN w='1*120e-9' l='45e-9'
.ENDS MYAOI

*.SUBCKT GenProp A B Gen Prop vdd vss
    *xAND A B NGen  vdd vss MYNAND
    *xNOT NGen Gen vdd vss MYNOT
    *xXOR A B Prop vdd vss MYXOR
*.ENDS Genprop

*.SUBCKT DotOperator Gen1 Prop1 Gen2 Prop2 GenOut PropOut vdd vss
    *xNAND Prop1 Prop2 NPropOut vdd vss MYNAND
    *xNOT1 NPropOut PropOut vdd vss MYNOT

    *xAOI Gen2 Prop2 Gen1 NGenOut vdd vss MYAOI
    *xNOT2 NGenOut GenOut vdd vss MYNOT
*.ENDS DotOperator

*** Own Subcricuits ***
.SUBCKT MYNOTSLOW input output vdd vss multfac='1'
    xM1 output input vss vss MOSN w='multfac*120e-9' l='45e-9'
    xM2 output input vdd vdd MOSP w='multfac*1*120e-9' l='45e-9'
.ENDS MYNOTSLOW

.SUBCKT GenPropInv A B Gen_inv Prop_inv vdd vss
    xAND A B Gen_inv  vdd vss MYNAND
    xXNOR A B Prop_inv vdd vss MYXNOR
.ENDS GenpropInv

.SUBCKT DotOperatorUpper Gen1 Prop1 Gen2 Prop2 NGenOut NPropOut vdd vss multfac='1'
    xNAND Prop1 Prop2 NPropOut vdd vss MYNAND multfac=multfac

    xAOI Gen2 Prop2 Gen1 NGenOut vdd vss MYAOI multfac=multfac
.ENDS DotOperatorUpper

.SUBCKT DotOperatorUpperInv Gen1 Prop1 Gen2 Prop2 GenOut PropOut vdd vss multfac='1'
    xAOI Gen1 Gen2 Prop2 GenOut vdd vss MYOAI multfac=multfac

    xXNOR Prop2 Prop1 PropOut vdd vss MYNOR multfac=multfac
.ENDS DotOperator

.SUBCKT DotOperatorLower Gen1 Gen2 Prop2 GenOut vdd vss multfac='1'
    xAOI Gen1 Gen2 Prop2 GenOut vdd vss MYAOI multfac=multfac
.ENDS DotOperatorLower

.SUBCKT DotOperatorLowerInv Gen1 Gen2 Prop2 GenOut vdd vss multfac='1'
    xOA Gen1 Gen2 Prop2 GenOut vdd vss MYOAI multfac=multfac
.ENDS DotOperatorLowerInv

.SUBCKT MYXNOR inputA inputB  output vdd vss multfac='1'
    xNOT1 inputA inputNA vdd vss MYNOTSLOW
    xNOT2 inputB inputNB vdd vss MYNOTSLOW

    xM1 np1     inputA  vdd vdd MOSP w='multfac*1*120e-9' l='45e-9'
    xM2 np1     inputNB  vdd vdd MOSP w='multfac*1*120e-9' l='45e-9'
    xM3 output  inputNA np1 vdd MOSP w='multfac*1*120e-9' l='45e-9'
    xM4 output  inputB np1 vdd MOSP w='multfac*1*120e-9' l='45e-9'

    xM5 output  inputNA  nn1 vss MOSN w='multfac*1*120e-9' l='45e-9'
    xM6 output  inputA nn2 vss MOSN w='multfac*1*120e-9' l='45e-9'
    xM7 nn1     inputB  vss vss MOSN w='multfac*1*120e-9' l='45e-9'
    xM8 nn2     inputNB vss vss MOSN w='multfac*1*120e-9' l='45e-9'
.ENDS MYXNOR

.SUBCKT MYOAI A B C Out vdd vss multfac='1'
    xM1 np1     B  vdd vdd MOSP w='1*120e-9' l='45e-9'
    xM2 Out     C  np1 vdd MOSP w='1*120e-9' l='45e-9'
    xM3 Out     A  vdd vdd MOSP w='multfac*2*120e-9' l='45e-9'
    
    xM5 Out     A  nn1 vss MOSN w='multfac*2*120e-9' l='45e-9'
    xM6 nn1     C  vss vss MOSN w='1*120e-9' l='45e-9'
    xM7 nn1     B  vss vss MOSN w='1*120e-9' l='45e-9'
.ENDS MYOAI

.SUBCKT MYNOR inputA inputB output vdd vss multfac='1'
    xM3 np1    inputB vdd vdd MOSP w='multfac*120e-9*2' l='45e-9'
    xM4 output inputA np1 vdd MOSP w='multfac*120e-9*2' l='45e-9'
    xM1 output inputA vss vss MOSN w='multfac*120e-9*2' l='45e-9'
    xM2 output inputB vss vss MOSN w='multfac*120e-9*2' l='45e-9'
.ENDS MYNOR

.SUBCKT MYTXOR inputA inputB output vdd vss multfac='1'
	xNOT1 inputB inputB_inv vdd vss MYNOTSLOW
    xM4 output inputA inputB vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM1 output inputA inputB_inv vss MOSN w='multfac*1*120e-9' l='45e-9'

    xM3 output inputB inputA vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM2 output inputB_inv inputA vss MOSN w='multfac*1*120e-9' l='45e-9'
.ENDS MYTXOR

.SUBCKT MYTXOR1 inputA inputB output vdd vss multfac='1'
	xNOT1 inputB inputB_inv vdd vss MYNOT
	xNOT2 inputA inputA_inv vdd vss MYNOT
    xM4 output inputB inputA vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM1 output inputB_inv inputA vss MOSN w='multfac*1*120e-9' l='45e-9'

    xM3 output inputB_inv inputA_inv vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM2 output inputB inputA_inv vss MOSN w='multfac*1*120e-9' l='45e-9'
.ENDS MYTXOR1

.SUBCKT MYTXNOR1 inputA inputB outputC vdd vss multfac='1'
	xNOT1 inputB inputB_inv vdd vss MYNOT
    xM4 output inputA inputB vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM1 output inputA inputB_inv vss MOSN w='multfac*1*120e-9' l='45e-9'

    xM3 output inputB inputA vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM2 output inputB_inv inputA vss MOSN w='multfac*1*120e-9' l='45e-9'

	xNOT2 output outputC vdd vss MYNOT
.ENDS MYTXNOR1

.SUBCKT MYTXNOR inputA inputB output vdd vss multfac='1'
	xNOT1 inputB inputB_inv vdd vss MYNOTSLOW
	xNOT2 inputA inputA_inv vdd vss MYNOTSLOW
    xM4 output inputB inputA_inv vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM1 output inputB_inv inputA_inv vss MOSN w='multfac*1*120e-9' l='45e-9'

    xM3 output inputB_inv inputA vdd MOSP w='multfac*2*120e-9' l='45e-9'
    xM2 output inputB inputA vss MOSN w='multfac*1*120e-9' l='45e-9'
.ENDS MYTXNOR

.END

