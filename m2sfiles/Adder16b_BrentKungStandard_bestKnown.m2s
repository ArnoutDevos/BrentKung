******************************
**** 16b Brent-Kung adder ****
******************************
.param supply = $globals.supply
.param halfsupply = $globals.supply/2


* Some simulation options
*-------------------------
.options post nomod
.option opts fast parhier=local

.lib '$< strcat(pwd, '/Resources/Technology/tech_wrapper.lib') >$ ' tt
.tran 0.005n 16n
.vec '$< strcat(pwd, '/m2sfiles/Adder16b.vec')>$'

.probe i

Vdd vdd vss supply
Vdd2 vdd2 vss supply
Vss vss 0 0 

* Actual circuit
*----------------
$ n = 16;
$ for j = 0:n-1
    xNOTa$j  a$j  aN$j     vdd vss MYNOT
    xNOTaN$j aN$j a_buff$j vdd vss MYNOT
    xNOTb$j  b$j  bN$j     vdd vss MYNOT
    xNOTbN$j bN$j b_buff$j vdd vss MYNOT
$ end


Xadder $xbus('a_buff',0:15) $xbus('b_buff',0:15) $xbus('s',0:16) vdd vss ADDER

   
$ for i = 1:16
    xNOT$i  s$i sN$i vdd2 vss MYNOT multfac = 16
$ end



* Brent-Kung Adder subcircuit
*-----------------------------
.SUBCKT ADDER a0 a1 a2 a3 a4 a5 a6 a7 a8 a9 a10 a11 a12 a13 a14 a15 b0 b1 b2 b3 b4 b5 b6 b7 b8 b9 b10 b11 b12 b13 b14 b15 s0 s1 s2 s3 s4 s5 s6 s7 s8 s9 s10 s11 s12 s13 s14 s15 s16 vdd vss

xGenProp0  a0  b0  gen0  prop0  vdd vss GenProp
$ for i = 1:15
xGenProp$i  a$i  b$i  gen$i  prop$i  vdd vss GenPropLeft
$ end

$ for i = 0:7
xDotOperator_a_$i  gen$i*2  prop$i*2  gen$i*2+1  prop$i*2+1  gen$<i*2+1>$_$i*2  prop$<i*2+1>$_$i*2  vdd vss DotOperator
$ end

    xDotOperator_b_0 gen1_0 gen3_2 prop3_2 gen3_0 vdd vss DotOperatorLower
    xDotOperator_b_1 gen5_4 prop5_4 gen7_6 prop7_6 gen7_4 prop7_4 vdd vss DotOperator
    xDotOperator_b_2 gen9_8 prop9_8 gen11_10 prop11_10 gen11_8 prop11_8 vdd vss DotOperator
    xDotOperator_b_3 gen13_12 prop13_12 gen15_14 prop15_14 gen15_12 prop15_12 vdd vss DotOperator

    xDotOperator_c_0 gen3_0 gen7_4 prop7_4 gen7_0 vdd vss DotOperatorLower
    xDotOperator_c_1 gen11_8 prop11_8 gen15_12 prop15_12 gen15_8 prop15_8 vdd vss DotOperator

    xDotOperator_d_0 gen7_0 gen15_8 prop15_8 gen15_0 vdd vss DotOperatorLower

    xDotOperator_l_1 gen7_0 gen11_8 prop11_8 gen11_0 vdd vss DotOperatorLower

    xDotOperator_m_1 gen3_0 gen5_4 prop5_4 gen5_0 vdd vss DotOperatorLower
    xDotOperator_m_2 gen7_0 gen9_8 prop9_8 gen9_0 vdd vss DotOperatorLower
    xDotOperator_m_3 gen11_0 gen13_12 prop13_12 gen13_0 vdd vss DotOperatorLower
	
    xDotOperator_n_1 gen1_0 gen2 prop2 gen2_0 vdd vss DotOperatorLower
    xDotOperator_n_2 gen3_0 gen4 prop4 gen4_0 vdd vss DotOperatorLower
    xDotOperator_n_3 gen5_0 gen6 prop6 gen6_0 vdd vss DotOperatorLower
    xDotOperator_n_4 gen7_0 gen8 prop8 gen8_0 vdd vss DotOperatorLower
    xDotOperator_n_5 gen9_0 gen10 prop10 gen10_0 vdd vss DotOperatorLower
    xDotOperator_n_6 gen11_0 gen12 prop12 gen12_0 vdd vss DotOperatorLower
    xDotOperator_n_7 gen13_0 gen14 prop14 gen14_0 vdd vss DotOperatorLower


xXOR_0 prop0 vss    s0 vdd vss MYXOR
xXOR_1 prop1 gen0   s1 vdd vss MYXOR
$ for i = 2:14
xXOR_$i  prop$i  gen$<i-1>$_0  s$i  vdd vss MYXOR
$ end
xXOR_15 prop15 gen14_0 s15 vdd vss MYXOR
xXOR_16 vss   gen15_0 s16 vdd vss MYXOR
.ENDS ADDER




* Other subcircuits
*-------------------

.SUBCKT MYNAND inputA inputB output vdd vss multfac='1'
    xM1 output inputA int int MOSN w='multfac*120e-9*2' l='45e-9'
    xM2 int    inputB vss vss MOSN w='multfac*120e-9*2' l='45e-9'
    xM3 output inputA vdd vdd MOSP w='multfac*120e-9*2' l='45e-9'
    xM4 output inputB vdd vdd MOSP w='multfac*120e-9*2' l='45e-9'
.ENDS MYNAND

.SUBCKT MYNOT input output vdd vss multfac='1'
    xM1 output input vss vss MOSN w='multfac*120e-9' l='45e-9'
    xM2 output input vdd vdd MOSP w='multfac*2*120e-9' l='45e-9'
.ENDS MYNOT

.SUBCKT MYXOR inputA inputB  output vdd vss multfac='1'
    xNOT1 inputA inputNA vdd vss MYNOT
    xNOT2 inputB inputNB vdd vss MYNOT

    xM1 np1     inputA  vdd vdd MOSP w='multfac*4*120e-9' l='45e-9'
    xM2 np1     inputB  vdd vdd MOSP w='multfac*4*120e-9' l='45e-9'
    xM3 output  inputNA np1 vdd MOSP w='multfac*4*120e-9' l='45e-9'
    xM4 output  inputNB np1 vdd MOSP w='multfac*4*120e-9' l='45e-9'

    xM5 output  inputA  nn1 vss MOSN w='multfac*2*120e-9' l='45e-9'
    xM6 output  inputNA nn2 vss MOSN w='multfac*2*120e-9' l='45e-9'
    xM7 nn1     inputB  vss vss MOSN w='multfac*2*120e-9' l='45e-9'
    xM8 nn2     inputNB vss vss MOSN w='multfac*2*120e-9' l='45e-9'
.ENDS MYXOR

.SUBCKT MYXOR2 inputA inputB  output vdd vss multfac='1'
    
.ENDS MYXOR2

.SUBCKT MYNOR inputA inputB  output vdd vss multfac='1'
	xM1 np1     inputB  vdd vdd MOSP w='multfac*4*120e-9' l='45e-9'
	xM2 output     inputA  np1 vdd MOSP w='multfac*4*120e-9' l='45e-9'

	xM3 output  inputB  vss vss MOSN w='multfac*1*120e-9' l='45e-9'
	xM4 output  inputA  vss vss MOSN w='multfac*1*120e-9' l='45e-9'
.ENDS MYNOR

.SUBCKT MYOR A B output vdd vss
    xNOR A B in  vdd vss MYNOR
    xNOT in output vdd vss MYNOT
.ENDS MYOR

.SUBCKT GenProp A B Gen Prop vdd vss
    xNAND A B NGen  vdd vss MYNAND
    xNOT NGen Gen vdd vss MYNOT
    xXOR A B Prop vdd vss MYXOR
.ENDS Genprop

.SUBCKT GenPropLeft A B Gen Prop vdd vss
    xNAND A B NGen  vdd vss MYNAND
    xNOT NGen Gen vdd vss MYNOT
    xOR A B Prop vdd vss MYOR
.ENDS GenpropLeft

.SUBCKT DotOperator Gen1 Prop1 Gen2 Prop2 GenOut PropOut vdd vss multfac ='1'
	xNOT1 Gen2 INV1out vdd vss MYNOT multfac=multfac
	xNAND1 Prop2 Gen1 NAND1out vdd vss MYNAND multfac=multfac

	xNAND3 INV1out NAND1out GenOut vdd vss MYNAND

	xNAND2 Prop2 Prop1 NAND2out vdd vss MYNAND
	xNOT2 NAND2out PropOut vdd vss MYNOT
.ENDS DotOperator

.SUBCKT MYAOI A B C Out vdd vss multfac='1'
    xM1 np1     B  vdd vdd MOSP w='multfac*4*120e-9' l='45e-9'
    xM2 np1     C  vdd vdd MOSP w='multfac*4*120e-9' l='45e-9'
    xM3 Out     A  np1 vdd MOSP w='multfac*4*120e-9' l='45e-9'
    
    xM5 Out     A  vss vss MOSN w='multfac*1*120e-9' l='45e-9'
    xM6 Out     C  nn1 vss MOSN w='multfac*2*120e-9' l='45e-9'
    xM7 nn1     B  vss vss MOSN w='multfac*2*120e-9' l='45e-9'
.ENDS MYAOI

.SUBCKT DotOperatorLower Gen1 Gen2 Prop2 GenOut vdd vss
	*xAOI Gen2 Gen1 Prop2 GenIn vdd vss MYAOI
	*xNOT GenIn GenOut vdd vss MYNOT
	xNOT1 Gen2 INV1out vdd vss MYNOT
	xNAND1 Prop2 Gen1 NAND1out vdd vss MYNAND

	xNAND3 INV1out NAND1out GenOut vdd vss MYNAND multfac=1.73
.ENDS DotOperatorLower

.END
* Hook all bulks to its sources!

