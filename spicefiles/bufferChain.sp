**********************************
**** INVERTER: simple example
**********************************

* Simulation setup
*------------------
*Some simulation options
.options post nomod
.option opts fast parhier=local

*Add the 45nm CMOS library. Notice how the path is generated by MATLAB
.lib '/users/start2012/r0305883/Master2/DDIS/DDIS1/Resources/Technology/tech_wrapper.lib ' tt

*Add the .vec file
.vec '/users/start2012/r0305883/Master2/DDIS/DDIS1/m2sfiles/bufferChain.vec'

.tran 0.001n 8n *Do a transient simulation, final time = 5ns
.probe v i

.param supply = 1


* Voltage sources
*-----------------
Vdd vdd vss supply
Vss vss 0 0 


* Input invertor
*----------------
xNot0 in0 in1 vdd vss MYNOT multfac = '1'

* Own circuits
*--------------
xNot1 in1 in2 vdd vss MYNOT multfac = '3.1'
xNot2 in2 out vdd vss MYNOT multfac = '9.3'
*.connect out in1

* Load invertor
*---------------
xNot99 out out1 vdd vss MYNOT multfac = '100'

* Subcircuits
*-------------
.SUBCKT MYNOT input output vdd vss multfac='1'
xM1 output input vss vss MOSN w='multfac*120e-9' l='45e-9'
xM2 output input vdd vdd MOSP w='multfac*2*120e-9' l='45e-9'
.ENDS MYNOT

.END


